0010 REM *** MISSILES ; 20-140 programme principal ***
0020 CLEAR ,,3:GOSUB 3010
0030 CLS:S=0:N=0:B=40:GOSUB 2050
0040   FOR I=0 TO 39
0050     LOCATE I,20:PRINT GR$(1)
0060     FOR J=1 TO 10:E$=INKEY$:IF E$="F" THEN GOSUB 1010:NEXT J
0070     LOCATE I,20:PRINT " "
0080   NEXT I
0090   FOR I=39 TO 0 STEP -1
0100     LOCATE I,20:PRINT GR$(1)
0110     FOR J=1 TO 10:E$=INKEY$:IF E$="F" THEN GOSUB 1010:NEXT J
0120     FOR J=1 TO 20:NEXT J:LOCATE I,20:PRINT " "
0130   NEXT I
0140 GOTO 40
1000 REM *** 1010-1120 sous programme de tir ***
1010 PLAY "A0T1L4O4":B=B-1
1020 COLOR 6,0:LOCATE 35,22,0:PRINT B:COLOR 2,0
1030 FOR J=19 TO 1 STEP -1
1040   LOCATE I,J,0:PRINT GR$(2)
1050   PLAY A$(J)
1060   LOCATE I,J,0:PRINT " "
1070 NEXT J
1080 IF POINT(4+8*I,4)<>1 THEN GOSUB 1510:RETURN
1090 LOCATE I,0,0:PRINT " ":COLOR 6,0
1100 FOR J=1 TO 5:S=S+1:LOCATE 7,22,0:PRINT S:PLAY B$+C$:NEXT J
1110 N=N+1:COLOR 2,0:GOSUB 1510
1120 RETURN
1500 REM *** 1510-1580 sous-programme de test ***
1510 IF B=0 THEN 2010
1520 IF N<>20 THEN RETURN
1530 LOCATE 15,22,0:COLOR 1,0:PRINT "BONUS: ";BONUS
1540 S=S+BONUS:FOR J=1 TO 10:BEEP:FOR K=1 TO 50:NEXT K:NEXT J
1550 LOCATE 15,22,0:PRINT"
1560 N=0:B=30
1570 GOSUB 2050
1580 RETURN
2000 REM *** 2010-2110 sous-programme affichage ***
2010 LOCATE 0,10:INPUT "Voulez-vous rejouer ?"; REP$
2020 IF LEFT$(REP$,1)="O" OR RsEP$="" THEN 30
2030 IF LEFT$(REP$,1)="N" THEN CLS:END
2040 GOTO 2010
2050 FOR I=0 TO 38 STEP 2
2060   LOCATE I,0:COLOR 1,0:PRINT GR$(0)
2070 NEXT I
2080 COLOR 5,0:LOCATE 0,22:PRINT "SCORE: ":LOCATE 25,22:PRINT "MISSILES: "
2090 COLOR 6,0:LOCATE 7,22:PRINT S
2100 LOCATE 35,22:PRINT B:COLOR 2,0
2110 RETURN
3000 REM *** 3010-3270 sous-programme intialisation et presentation ***
3010 CLS:DIM A$(26):BONUS=50
3020 SCREEN 2,0,0
3030 C$="DODO#RERE#MIFAFA#SOSO#LALA#SI"
3040 B$="A0T1L4"
3050 FOR I=1 TO 21
3060   READ A$(I)
3070 NEXT I
3080 DATA "SO","FA#", "FA", "MI", "RE#","RE", "DO#", "DO", "O5", "SI", "LA#", "LA", "SO#","SO", "FA#", "FA", "MI", "RE#", "RE", "DO#", "DO"
3090 DEFGR$(0)=60,126,219,255,255,153,153,153
3100 DEFGR$(1)=24,24,24,60,126,255,189,153
3110 DEFGR$(2)=24,24,60,102,189,153,24,60
3120 ATTRB 1,1:COLOR 1,0
3130 LOCATE 10,12:PRINT "MISSILES"
3140 LOCATE 0,2:COLOR 1,0
3150 FOR I=0 TO 9
3160   PRINT GR$(0);" ";
3170 NEXT I
3480 COLOR 2,0:LOCATE 0,22:PRINT GR$(1)
3190 FOR I=20 TO 2 STEP -2
3200   LOCATE 0,I,0:COLOR 3,0:PRINT GR$(2)
3210   PLAY "L2DO"
3220   LOCATE 0,I,0:PRINT " "
3230 NEXT I
3240 ATTRB 0,0:COLOR 2,0:FOR J=1 TO 10:PLAY B$+C$:NEXT J
3250 LOCATE 0,23: PRINT "(touche F pour tirer)"
3260 LOCATE 0,22: INPUT "Tapez ENTREE pour commencer",Q$
3270 RETURN
