1 ' Peek memoire
2 ' Livre "La face cachee du MO5"
3 ' de Jean-Baptiste Touchard, 1985
4 ' voir page 45
5 '
5 HX$="0123456789ABCDEF"
10 CLS:SCREEN 7,0,0
20 INPUT "Adresse de debut : "; A$
30 A=VAL(A$)
40 CLS:SCREEN 7,0,0:LOCATE 0,2
50 FOR M=A TO A+158 STEP 8
55 Z=M:GOSUB 1000:M$=Z$
60 IF LEN(M$)<4 THEN M$="0"+M$:GOTO 60
65 COLOR 7,0
66 PRINT M$;" ";
70 D$=""
75 FOR M1=0 to 7
80 D=PEEK(M+M1)
85 D$=D$+CHR$(D)
90 Z=D:GOSUB 1000:O$=Z$
91 IF LEN(O$)=1 THEN O$=" "+O$
92 PRINT O$;" ";
95 NEXT M1
100 FOR I=1 TO 8
105 D=ASC(MID$(D$,I,1))
110 COLOR 7,0
115 IF D>127 THEN D=D-128:COLOR 6
120 IF D<32 THEN D=D+64:COLOR ,4
125 PRINT CHR$(D);
130 NEXT I
140 PRINT
150 NEXT M
160 K$=INKEY$
161 K$=INKEY$:IF K$="" THEN 161
165 IF K$="A" THEN 10
170 A=A+160:GOTO 40
1000 ' Conversion HEX$
1010 Z1=Z:Z$=""
1015 Z2=INT(Z1/16):Z1=Z1-Z2*16
1020 Z$=MID$(HX$,1+Z1,1)+Z$
1040 IF Z2>0 THEN Z1=Z2:GOTO 1015
1045 RETURN
