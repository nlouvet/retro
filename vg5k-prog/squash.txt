10 REM **********
20 REM * Squash *
30 REM **********
40 CLEAR 2000
50 GOSUB 5000
60 REM Afficher cadre, raquette, balle
70 GOSUB 5200
80 REM Programme principal
90 FIN=0
100 GOSUB 600
110 REM Deplacement de la balle
120 CB=CF:GOSUB 500
130 IF XB+HB>XR GOTO 150
140 GOSUB 700:GOTO 230
150 IF YB+VB>1 GOTO 170
160 IF YB>1 THEN YB=1:VB=-VB:PLAY "T1O1A":GOTO 230
170 IF YB+VB<23 GOTO 190
180 IF YB<23 THEN YB=23:VB=-VB:PLAY "T1O1A":GOTO 230
190 IF XB+HB<39 GOTO 210
200 XB=38:HB=-HB:PLAY "T1O1A":GOTO 230
210 XB=XB+HB:YB=YB+VB
220 CB=0:GOSUB 500
230 IF FIN<>0 GOTO 1000
240 GOTO 100
500 REM Affichage de la balle
510 ET CB,0,0:CURSORX XB:CURSORY YB
520 PRINT CHR$(126)
530 DISPLAY 10
540 RETURN
600 REM Deplacement de la raquette
610 IF STICKY(2)=0 THEN RETURN
620 IF STICKY(2)=1 THEN VR=+1
630 IF STICKY(2)=255 THEN VR=-1
640 IF YR+ER*VR=1 THEN RETURN
650 IF YR+ER*VR=23 THEN RETURN
660 TX CF,0,0:CURSORX XR:CURSORY YR-ER*VR:PRINT CHR$(127)
670 YR=YR+VR
680 TX 1,0,0:CURSORX XR:CURSORY YR+ER*VR:PRINT CHR$(127)
690 RETURN
700 REM Test de rebond sur la raquette
710 IF YB<YR-ER THEN FIN=1:RETURN
720 IF YB>YR+ER THEN FIN=1:RETURN
730 REM Rebobd de la balle
740 SR=SR+1:PLAY "t10o3a4":HB=INT(RND(1)*6+1)
750 IF (YB-YR-ER) OR (YB=YR+ER) GOTO 780
760 CB=0:GOSUB 500:RETURN
770 REM Rebond aleatoire
780 ON INT(3*RND(1)+1) GOTO 790,800,810
790 VB=3:IF RND(1)<0.5 THEN VB=-VB:GOTO 820
800 VB=2:IF RND(1)<0.5 THEN VB=-VB:GOTO 820
810 VB=1:IF RND(1)<0.5 THEN VB=-VB
820 CB=0:GOSUB 500
830 RETURN
1000 REM Fin de manche
1010 PLAY "T10O1GGFFEEDDCCBBAA"
1020 TX 1,0,0:CURSORX 10:CURSORY 8
1030 PRINT "score: ";SR
1040 TX 1,0,0:CURSORX 10:CURSORY 10
1050 PRINT "fin de la manche No ";4-NB
1060 NB=NB-1
1070 IF NB=0 GOTO 10000
1080 PRINT:PRINT "RET pour la balle suivante":INPUT R$
1090 IF SR<35-10*NB GOTO 60
1100 REM Changement de la taille de la raquette
1110 IF TR>=3 THEN TR=TR-2
1120 GOTO 60
5000 REM Presentation
5010 NB=3:SR=0:TR=5
5020 INIT 4,4:TX 3,7,0
5030 CURSORX 10:CURSORY 10
5040 PRINT "s q u a s h "
5050 CURSORX 10:CURSORY 11
5060 PRINT "s q u a s h "
5070 TX 0,0,0:PRINT
5080 PRINT "RET pour la premiere balle":INPUT R$
5090 SETET 126,"003C7EFFFFFFFF7E3C00"
5100 RETURN
5200 REM Lancement de la balle et trace du cadre
5210 CF=7:INIT CF,7:TX 4,0,0:PAGE
5220 CURSORX 1:CURSORY 0:DELIM 4,4,0
5230 CURSORX 1:CURSORY 24:DELIM 4,4,0
5240 FOR Y=1 TO 24
5250 CURSORX 39:CURSORY Y:PRINT CHR$(127)
5260 NEXT Y
5270 REM Initialisaitons de la raquette
5280 XR=7:YR=12:VR=0:ER=(TR-1)/2
5290 TX 1,0,0:CURSORY YR-ER
5300 FOR I=1 TO TR
5310 CURSORX XR:PRINT CHR$(127)
5320 NEXT I
5330 REM Initialisations de la balle
5340 XB=38:YB=INT(RND(1)*16+4):HB=-INT(RND(1)*3+1)
5350 ON INT(3*RND(1)+1) GOTO 5360,5370,5380
5360 VB=3:IF RND(1)<0.5 THEN VB=-VB:GOTO 5390
5370 VB=2:IF RND(1)<0.5 THEN VB=-VB:GOTO 5390
5380 VB=1:IF RND(1)<0.5 THEN VB=-VB
5390 CB=0:GOSUB 500
5400 RETURN
10000 REM Fin de partie
10010 TX 0,0,0:PRINT:PRINT "Fin de la partie"
10020 PRINT:PRINT "Voulez-vous rejouer";CHR$(4);:INPUT R$
10030 IF R$="O" OR R$="o" THEN RUN
10040 INIT 6,6:END

